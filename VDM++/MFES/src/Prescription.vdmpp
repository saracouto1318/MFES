class Prescription

instance variables
  private medicaments:set of (Medicament);
  private code:Types`String;
  
  inv len code > 1;
operations
	-- Prescription constructor
	public Prescription: Types`String ==> Prescription
		Prescription(c) == (code := c; medicaments := {}; return self)
	pre len c > 1
	post code = c and medicaments = {};
	
	-- Returns the prescription's code
	pure public getCode : () ==> Types`String
		getCode() == (return code);
	
	-- Adds a medicament to the prescription's medicaments
	public addMedicament: Medicament ==> ()
		addMedicament(m) == (medicaments := {m} union medicaments)
	pre m not in set medicaments
	post m in set medicaments;
	
	-- Removes a medicament from the prescription's medicaments
	public removeMedicament: Medicament ==> ()
		removeMedicament(m) == (medicaments := medicaments \ {m})
	pre m in set medicaments
	post m not in set medicaments;
	
	-- Returns the prescription's medicaments
	pure public getMedicaments: () ==> set of (Medicament)
		getMedicaments() == (return medicaments);

end Prescription
