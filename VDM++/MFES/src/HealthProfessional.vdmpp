class HealthProfessional is subclass of Person

instance variables
  private medicalNumber: Utils`String;
  private specialties:set of (Specialty);
  private patients : set of(Patient);
	private type : Utils`Type;
	
	inv card patients >= 0;
  inv card specialties < 5;
	inv type <> nil;
operations
	public HealthProfessional: Utils`String * Utils`String * Utils`String * Utils`String * Utils`String * Utils`String * Utils`Type ==> HealthProfessional
		HealthProfessional(a, fn, ln, c, pn, s, t) == (medicalNumber := s; type := t; specialties := {}; patients := {}; Person(a, fn, ln, c, pn))
	pre t <> nil
	post medicalNumber = s and type = t and specialties = {} and patients = {};
	
	pure public getMedicalNumber: () ==> Utils`String
		getMedicalNumber() == (return medicalNumber);
	
	pure public getSpecialties: () ==> set of (Specialty)
		getSpecialties() == (return specialties);
	
	pure public getPatients: () ==> set of (Patient)
		getPatients() == (return patients);
		
	pure public getType : () ==> Utils`Type
		getType() == (return type);
		
	public removeSpecialty: Specialty ==> ()
		removeSpecialty(s) == (specialties := specialties \ {s})
	pre s in set specialties
	post s not in set specialties;
		
	public addSpecialty: Specialty ==> ()
		addSpecialty(s) == (specialties := specialties union {s})
	pre s not in set specialties
	post s in set specialties;
	
	public addPatient : Patient ==> ()
		addPatient(p) == (patients :=  patients union {p})
	pre p not in set patients
	post p in set patients;
	
	public removePatient : Patient ==> ()
		removePatient(p) == (patients := patients \ {p})
	pre p in set patients
	post p not in set patients;

end HealthProfessional
