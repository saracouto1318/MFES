class Appointment is subclass of Task

instance variables
  private priority : Types`Priority;
  
  inv priority <> nil;
  inv medicalAssoc.getType() = <Doctor>;
operations
	-- Appointment constructor
	public Appointment: HealthProfessional * Schedule * Patient * Hospital==> Appointment
		Appointment(d, s, p, h) == (medicalAssoc := d; priority := <Medium>; Task(d, s, p, h, <Appointment>))
	post medicalAssoc = d and priority = <Medium>;
	
	-- Urgency Apppointment constructor
	public Appointment: HealthProfessional * Types`Priority * Schedule * Patient * Hospital ==> Appointment
		Appointment(d, p, s, pat, h) == (medicalAssoc := d; priority := p; Task(d, s, pat, h, <Urgencies>))
	pre p <> nil
	post medicalAssoc = d and priority = p;

	-- Returns the appointment's priority
	pure public getPriority : () ==> Types`Priority
		getPriority() == (return priority);

  -- Sets the appointment's priority
  public setPriority : Types`Priority ==> ()
  	setPriority(p) == (priority := p)
  pre taskType = <Urgencies>;
  
end Appointment
